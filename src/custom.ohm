Custom {
  Program       =  Block*
  Block         =  Statement+
  Statement     =  (Loop | FunctionCall | Declaration | Assignment | FunctionDeclaration | Print  | Return) (";")?  --declarative
                | "if" "(" Exp ")" "{" Block "}"
                  ("else" "if" "(" Exp ")" "{" Block "}" )*
                  ("else" "{" Block "}")?                     -- if
  
  Loop 			    = "while" "(" Exp ")" "{" Block* "}" --while
                | "for" "(" Declaration ";" Exp ";" Assignment  ")" "{" Block* "}"  --for
  FunctionCall  =  id "(" Args ")"
  Declaration   =  type Assignment
  Assignment    = id "=" Exp						                      -- assign
                | id "++"							                        -- increment
                | id "--"								                      -- decrement
  FunctionDeclaration =  type id "(" ListOf<Param, ","> ")" "{" Block* "}"

  Args          =  ListOf<BinExp, ",">
  Param         =  type id 

  Exp           =  Exp relop BinExp                           -- binary
                |  BinExp "?" BinExp ":" BinExp               -- ternary
                |  BinExp
  BinExp        =  BinExp binop AddExp                        -- binary
                |  AddExp
  AddExp        =  AddExp addop MullExp                       -- binary
                |  MullExp
  MullExp       =  MullExp mullop PrefixExp                   -- binary
                |  PrefixExp
  PrefixExp     =  prefixop ExpoExp                           -- binary
                |  ExpoExp
  ExpoExp       =  ParenExp expop ExpoExp                     -- binary
                |  ParenExp
  ParenExp      =  "(" Exp ")"                                -- parens
                |  Array
                |  bool
                |  numlit
                |  stringlit
                | id
  
  Print         =  "print" "(" Exp ")"
  Return        =  "return" ParenExp
  Array          =  "[" (BinExp ("," BinExp)*)? "]"

  type          =  "string" | "char" | "bool" | "int" | "float"
  keyword       =  (type | bool | "if" | "else" | "return" | "print") ~alnum
  id            =  ~keyword letter (alnum)*
  prefixop      =  "!" | "-"
  relop         =  ">" | ">=" | "==" | "!=" | "<" | "<="
  addop         =  "+" | "-" 
  mullop        =  "*" | "/" | "%"
  expop         =  "^"
  binop         =  "||" | "&&"
  bool          = "true" | "false"
  numlit        =  digit+ ("." digit+)?
  stringlit     =  "\"" (char | "\'")* "\""
                | "\'" (char | "\"")* "\'"
  char          =  escape
                |  ~"\\" ~"\"" ~"\'" ~"\\n" any
  escape        = "\\\\" | "\\\"" | "\\'" | "\\n" | "\\t"
                |  "\\u{" hexDigit+ "}"                       -- codepoint
  space         := " " | "\t" | "\n" | comment
  comment       =  "//" (~"\n" any)*                          --singleline
                | "/*" (~("*/") any )* "*/"                   --multiline
}